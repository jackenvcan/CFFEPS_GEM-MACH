!---------------------------------- LICENCE BEGIN -------------------------------
! GEM-MACH - Atmospheric chemistry library for the GEM numerical atmospheric model
! Copyright (C) 2007-2013 - Air Quality Research Division &
!                           National Prediction Operations division
!                           Environnement Canada
! This library is free software; you can redistribute it and/or
! modify it under the terms of the GNU Lesser General Public
! License as published by the Free Software Foundation; either
! version 2.1 of the License, or (at your option) any later version.
!
! This library is distributed in the hope that it will be useful,
! but WITHOUT ANY WARRANTY; without even the implied warranty of
! MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU
! Lesser General Public License for more details.
!
! You should have received a copy of the GNU Lesser General Public
! License along with this library; if not, write to the Free Software
! Foundation, Inc., 51 Franklin Street, Fifth Floor, Boston, MA  02110-1301  USA
!---------------------------------- LICENCE END ---------------------------------

!============================================================================!
!         Environnement Canada         |        Environment Canada           !
!                                      |                                     !
! - Service meteorologique du Canada   | - Meteorological Service of Canada  !
! - Direction generale des sciences    | - Science and Technology Branch     !
!   et de la technologie               |                                     !
!============================================================================!
!                            http://www.ec.gc.ca                             !
!============================================================================!
!
!
! Projet/Project : GEM-MACH
! Fichier/File   : mach_cam_utils_mod.ftn90
! Creation       : P. Huang, Apr. 2008
! Description    :
!
!
!============================================================================

module mach_cam_utils_mod
   use chm_utils_mod
   use chm_ptopo_grid_mod, only: chm_ni, chm_nk

   implicit none
   save
   real(kind=4) boltzk, delt

   integer(kind=4) idebug, idrydep, iincld, iblcld

   real(kind=4)     cam       ! cam version also a plag to turn on (10.) or off (1.) online sulfate chemistry scheme
   integer(kind=4)  imm       ! flag for mixing ratio output in kg/kg
   integer(kind=4)  ima       ! flag for mass conc output in ug/m**3
   integer(kind=4)  inu       ! flag for number conc. output
   integer(kind=4)  icob      ! size bin number with which coagulation will apply

   integer(kind=4)  icoag     ! time step interval for calculation of coagulation coefficient for PM
   integer(kind=4)  i_ss      ! switch for scheme of sea-salt surface flux (GONG_MONAHAN'S SCHEME)
   integer(kind=4)  isize     ! number of size bins

   integer(kind=4), parameter :: maxnsg = 12  ! No. of gas/part species (12)
   integer(kind=4), parameter :: maxnsaq = 13 ! No. of Aqueous species  (13)
   integer(kind=4), parameter :: maxcnz = 1
   integer(kind=4), parameter :: maxnsp = 9
   integer(kind=4), parameter :: maxns = 47   ! No of gas-phase chemistry species


   contains
!============================================================================
! Name           : sigmacal
!
! Creation       : P. Huang, Mar. 2008 for adapting AURAMS version CAM
!                  P. Huang, Dec. 2007 for GEM-MACH
!
! Description    : 1. Calculate interface values of sigma for CAM model
!                  2. Check tracer mass for CAM model
!
! Extra info     : Varify size bin configration
!
! Arguments:  IN
!               sig            -> Local sigma values
!               ni             -> No. x-direction (W-E) gridpoints
!               nk             -> No. z-direct. vertical levels
!
!             OUT
!               SHTJ          -> Local interface (top) sigma value
!               DSHJ          -> Sigma difference of bottom and top of a layer
!
!============================================================================

   subroutine sigmacal(SHTJ, DSHJ, sig)

      implicit none
      real(kind=4),    intent(in)  :: sig (chm_ni, chm_nk)
      real(kind=4),    intent(out) :: shtj(chm_ni, chm_nk+1), dshj(chm_ni, chm_nk)
      integer(kind=4)              :: i, k

      do i = 1, chm_ni
         shtj(i, 1) = sig(i, 1)
         do  k = 2, chm_nk
            shtj(i, k) = (sig(i, k) + sig(i, k-1)) / 2
         end do
!>>      shtj(i, chm_nk + 1) = sig(i, chm_nk)    sg: I assume that this was done because sig(chm_nk) was
!>>                                                  expected to contain the surface (ie sig=1) therefore
!>>                                                  I replace it by the following line
         shtj(i, chm_nk + 1) = 1.0
         do k = 1, chm_nk
            dshj(i, k) = shtj(i, k + 1) - shtj(i, k)
         end do
      end do

      return
   end subroutine sigmacal

!============================================================================
! Name           : sigmathck
!
! Creation       : V. Savic-Jovcic, Mar. 2016 adapted sigmacal into sigmadep
!
! Description    : 1. Calculate depth of model layer as a difference of sigma levels
!
! Arguments:  IN
!               sig       -> local sigma values
!
!             OUT
!               DSIG      -> thickness of the layers that have sig as the centers
!
!============================================================================

   subroutine sigmathck(dsig, sig)

      implicit none
      real(kind=4),    intent(in)  :: sig (chm_ni, chm_nk)
      real(kind=4),    intent(out) :: dsig(chm_ni, chm_nk)
      integer(kind=4)              :: i, k
      real(kind=4)                 :: shtj(chm_ni, chm_nk+1)

      do i = 1, chm_ni
         shtj(i, 1) = sig(i, 1)
         do  k = 2, chm_nk
            shtj(i, k) = (sig(i, k) + sig(i, k-1)) / 2
         end do
!>>      shtj(i, chm_nk + 1) = sig(i, chm_nk)    sg: P. Huang assumed that this was done because sig(chm_nk) was
!>>                                                  expected to contain the surface (ie sig=1) therefore
!>>                                                  it was replaced by the following line
         shtj(i, chm_nk + 1) = 1.0
         do k = 1, chm_nk
            dsig(i, k) = shtj(i, k + 1) - shtj(i, k)
         end do
      end do

      return
   end subroutine sigmathck

!============================================================================
! Name           : chk_trc
!
! Creation       : P. Huang, Mar. 2008 for CAM in GEM-MACH
!
! Description    : Check tracer value to avoid any unreasonable tracer value crashing run of CAM
!
! Arguments:  IN
!               trac            -> Tracer array to be checked
!               ik              -> ik = 0 tracer array without moon level; ik = 1 tracer array with moon level
!               chm_ni             -> No. x-direction (W-E) gridpoints
!               lev             -> No. z-direct. vertical levels
!               IL1             -> Start No. x-direct.
!               IL2             -> End No. x-direct.
!               ntr             -> Number of total tracers for trac
!               strt_trci       -> Start index of the tracers to be checked
!               end_trci        -> End index of the tracers to be checked
!               JLAT            -> J Slice number
!               where_str       -> Identifier shows location of this ckecking process applied
!               f_step          -> Time step in current
!               ipass           -> Identifier to print location of each call when ipass = 9
!
!             OUT
!
!============================================================================

   subroutine chk_trc(trac, ik, lev, IL1, IL2, ntr, strt_trci, end_trci, JLAT, where_str, f_step, ipass)

   implicit none
   integer(kind=4),     intent(in)  :: ik, lev, il1, il2, ntr, strt_trci, end_trci, jlat, f_step, ipass
   real(kind=4),        intent(in)  :: trac(chm_ni, lev, ntr)
   character(len=8),    intent(in)  :: where_str
   integer(kind=4)                  :: n, id, jd, itr, dlev, it, i, l
   logical(kind=4)                  :: ldebug
   external qqexit

      ldebug = .false.
      do n = strt_trci, end_trci
         do l = 1, lev - ik
            do i = il1, il2
               if (trac(i, l + ik, n) > 0.1 .or. trac(i, l + ik, n) < 0.0) then
                  ldebug = .true.
                  dlev = l
                  id = i
                  jd = jlat
                  itr = n
                  it = f_step
               end if
            end do
         end do
      end do
      if (ldebug) then
         write (0, *)' tracer concentration is too high or negative'
         write (0, *)' overflow is likely. program stops at:', where_str
         write (0, *)' tracer ', itr, '= ', trac(id, dlev + ik, itr)
         write (0, *)' dlev    id      jd    itr      it '
         write (0, *)dlev,    id,      jd,    itr,      it
         call qqexit(1)
      else
         if (ipass.eq.9) write (0, *)'tracer concentration checking is passed at:  ', where_str
      end if

      return
   end subroutine chk_trc

end module mach_cam_utils_mod
