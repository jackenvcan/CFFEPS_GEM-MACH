!---------------------------------- LICENCE BEGIN -------------------------------
! GEM-MACH - Atmospheric chemistry library for the GEM numerical atmospheric model
! Copyright (C) 2007-2013 - Air Quality Research Division &
!                           National Prediction Operations division
!                           Environnement Canada
! This library is free software; you can redistribute it and/or
! modify it under the terms of the GNU Lesser General Public
! License as published by the Free Software Foundation; either
! version 2.1 of the License, or (at your option) any later version.
!
! This library is distributed in the hope that it will be useful,
! but WITHOUT ANY WARRANTY; without even the implied warranty of
! MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU
! Lesser General Public License for more details.
!
! You should have received a copy of the GNU Lesser General Public
! License along with this library; if not, write to the Free Software
! Foundation, Inc., 51 Franklin Street, Fifth Floor, Boston, MA  02110-1301  USA
!---------------------------------- LICENCE END ---------------------------------

!============================================================================!
!         Environnement Canada         |        Environment Canada           !
!                                      |                                     !
! - Service meteorologique du Canada   | - Meteorological Service of Canada  !
! - Direction generale des sciences    | - Science and Technology Branch     !
!   et de la technologie               |                                     !
!============================================================================!
!                            http://www.ec.gc.ca                             !
!============================================================================!
!
! Projet/Project : GEM-MACH
! Fichier/File   : mach_tridiag.ftn90
! Creation       : Paul Makar, nov 2007
! Description    : Solution of the tridiagonal system of equations
!
! Extra info     : Based on Janusz Pudykiewicz, March 1995
!                  Follows algorithm found in "Numerical Recipes".
!                  Note that the potential exists for a division by zero error in the
!                  following, in that if array "bet" happens to = zero, then a division
!                  by zero error could occur.  However, the same algorithm has been
!                  used in AURAMS and CHRONOS for many years without encountering
!                  this error condition, and to add a check here would slow down the
!                  processing.
! Arguments:
!           IN
!              a, b, c, r -> matrix terms
!
!           OUT
!              bet ->
!              gam ->
!
!           INOUT
!              u   ->
!
!=============================================================================
!
!!if_on
subroutine mach_tridiag (a, b, c, r, bet, gam, u, ni_net)
   use chm_ptopo_grid_mod, only: chm_nk
!!if_off
   implicit none
!!if_on
   integer(kind=4), intent   (in) :: ni_net
   real(kind=4),    intent   (in) :: a  (ni_net,chm_nk)
   real(kind=4),    intent   (in) :: b  (ni_net,chm_nk)
   real(kind=4),    intent   (in) :: c  (ni_net,chm_nk)
   real(kind=4),    intent   (in) :: r  (ni_net,chm_nk)
   real(kind=4),    intent  (out) :: bet(ni_net)
   real(kind=4),    intent  (out) :: gam(ni_net,chm_nk)
   real(kind=4),    intent(inout) :: u  (ni_net,chm_nk)
!!if_off
!
! Local Variables
!
   integer(kind=4)  :: i, j
!
!--------------------------------------------------------------------------
!
!  solves for a vector u of length n the tridiagonal linear set:
!
!       b1  c1                        u1      r1
!       a2  b2  c2                 =  u2      r2
!
!
!                           aN  bN    uN      rN
!
!  a, b, c and r are input vectors and are not modified
!
!--------------------------------------------------------------------------
!
   do i = 1, ni_net
      bet(i) = b(i, 1)
      u(i, 1) = r(i, 1) /  bet(i)
   end do

      do j = 2, chm_nk
         do i = 1, ni_net
            gam(i, j) = c(i, j - 1) / bet(i)
            bet(i)    = b(i, j) - a(i, j) * gam(i, j)
            u  (i, j) = (r(i, j) - a(i, j) * u(i, j - 1)) / bet(i)
         end do
      end do
!
!  Backsubstitution
!
   do j = chm_nk - 1, 1, -1
      do i = 1, ni_net
         u(i, j) = u(i, j) - gam(i, j + 1) * u(i, j + 1)
      end do
   end do
   return
end
